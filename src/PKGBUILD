# Maintainer: Hunter Wittenborn <hunter@hunterwittenborn.com>

pkgname=mpm
pkgver=2.8.6
pkgrel=1
pkgdesc="Package manager for makedeb (git release)"
depends=('makedeb' 'makedeb-db' 'jq' 'asp')
arch=('any')
license=('GPL3')
url="https://github.com/hwittenborn/mpm"

# Used to obtain folder names for local repository
_gitdir=$(git rev-parse --show-toplevel)
_foldername=$(basename "${_gitdir}")
source=("git+file://${_gitdir}")
sha256sums=('SKIP')

package() {
  cd "${srcdir}/${_foldername}"

  # Set package version
  sed -i "s|mpm_version=.*|mpm_version='${pkgver}'|" src/mpm.sh

  # Create one file out of functions and mpm.sh
  mkdir -p "${pkgdir}/usr/bin/"
  echo '#!/usr/bin/env bash' > "${pkgdir}/usr/bin/mpm"

  for i in $(find src/functions/); do
    if ! [[ -d "${i}" ]]; then
      cat "${i}" >> "${pkgdir}/usr/bin/mpm"
    fi
  done
  cat src/mpm.sh >> "${pkgdir}/usr/bin/mpm"

  chmod 555 "${pkgdir}/usr/bin/mpm"

  # Remove commands used in testing
  sed -i 's|.*# REMOVE AT PACKAGING||g' "${pkgdir}/usr/bin/mpm"

  # Set up APT repo and other needed files
  mkdir -p "${pkgdir}/etc/mpm/repo/debs"
  touch "${pkgdir}/etc/mpm/sources.db"
}
